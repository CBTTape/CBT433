/* REXX    STAKEDIT   Recursive edit.  Will also Browse or View
                      depending on the active alias.
           Use '(routine name)  ?' for HELP-text.

        NOTE ::::    MUST    be called via the command table:
                     SELECT CMD(%STAKEDIT &ZPARM) NEWAPPL(ISR)
                     SELECT CMD(%STAKBROW &ZPARM) NEWAPPL(ISR)
                     SELECT CMD(%STAKVIEW &ZPARM) NEWAPPL(ISR)

           Written by Frank Clarke  rexxhead@yahoo.com long ago

     Impact Analysis
.    SYSEXEC   FCCMDUPD
.    SYSEXEC   TRAPOUT
.    (alias)   STAKBROW
.    (alias)   STAKVIEW

     Modification History
     20210418 fxc REXXSKEL at long last; INSTALL;
     20211024 fxc correct erroneous error message
     20230407 fxc adjust HELP
     20230806 fxc chg SYSPROC to SYSEXEC in Impact Analysis;
     20240415 fxc DUMP_QUEUE quiet;
     20250102 fxc use MEMLIST to deliver results;
     20250112 fxc housekeeping;
     20250119 fxc discriminate between PS and PO;
     20250308 fxc trap LISTDSI error;

*/ arg argline
address ISPEXEC                        /* REXXSKEL ver.20210402      */
arg parms "((" opts

signal on syntax
signal on novalue

call TOOLKIT_INIT                      /* conventional start-up     -*/
   info = Strip(opts,"T",")")          /* clip trailing paren        */
rc = Trace("O"); rc = Trace(tv)
info   = parms                         /* to enable parsing          */

parse var parms indsn .
x = LISTDSI(indsn  " directory norecall " )
if x > 0 then do
   say sysmsglvl1
   say sysmsglvl2
   exit
   end

icmd = exec_name                       /* How was I called ?         */
action.          = "BROWSE"
action.STAKEDIT  = "EDIT"
action.STAKVIEW  = "VIEW"
dflt.            = "V"
dflt.STAKBROW    = "B"
dflt.STAKEDIT    = "E"
dflt  = dflt.exec_name

"CONTROL ERRORS RETURN"                /* I'll handle my own         */
if sysdsorg = 'PS' then do
   if action.icmd = "BROWSE" then tail = ""
                             else tail = "PROFILE(DEFAULT)"
   (action.icmd) "DATASET("indsn")" tail
   end                                 /* PS                         */
if sysdsorg = 'PO' then do
   address TSO "MEMLIST"  indsn  dflt  "((" opts

   if rc > 8 then do
      zerrhm   = "ISR00000"
      zerralrm = "YES"
      "SETMSG MSG(ISRZ002)"
      end
   end                                 /* PO                         */

if \sw.nested then call DUMP_QUEUE 'quiet' /*                       -*/
exit                                   /*@ STAKEDIT                  */
/*
.  ----------------------------------------------------------------- */
LOCAL_PREINIT:                         /*@ customize opts            */
   address TSO

   if SWITCH("INSTALL") then do        /* set tmpcmds                */
      queue "ED"                       /* zctverb                    */
      queue "0"                        /* zcttrunc                   */
      queue "SELECT CMD(%STAKEDIT &ZPARM)"    /* zctact              */
      queue "Recursive Edit"                  /* zctdesc             */
      "FCCMDUPD"                       /* load the table             */
      queue "BR"                       /* zctverb                    */
      queue "0"                        /* zcttrunc                   */
      queue "SELECT CMD(%STAKBROW &ZPARM)"    /* zctact              */
      queue "Recursive Browse"                /* zctdesc             */
      "FCCMDUPD"                       /* load the table             */
      queue "VW"                       /* zctverb                    */
      queue "0"                        /* zcttrunc                   */
      queue "SELECT CMD(%STAKVIEW &ZPARM)"    /* zctact              */
      queue "Recursive View"                  /* zctdesc             */
      "FCCMDUPD"                       /* load the table             */
      exit
      end                              /* INSTALL                    */

return                                 /*@ LOCAL_PREINIT             */
/*   subroutines below LOCAL_PREINIT are not selected by SHOWFLOW    */
/*
.  ----------------------------------------------------------------- */
HELP:                                  /*@                           */
address TSO;"CLEAR"
if helpmsg <> "" then say helpmsg; say ""
ex_nam = Left(exec_name,8)             /* predictable size           */
say "  "ex_nam"      will recursively edit (STAKEDIT), browse (STAKBROW), or   "
say "                view (STAKVIEW) depending on the alias invoked.           "
say "                                                                          "
say "  Syntax:   "ex_nam"  dsname                                              "
say "               ((     INSTALL                                             "
say "                                                                          "
say "            dsname    specifies the dataset to be acted upon.             "
say "                      (TSO-format)                                        "
say "                                                                          "
say "            INSTALL   (switch in OPTS) causes 3 commands to be added to   "
say "                      the user's command table.  If INSTALL is requested, "
say "                      no other processing is done.  The command-verbs are "
say "                      ED, BR, and VW.                                     "
"NEWSTACK"; pull ; "CLEAR" ; "DELSTACK"
say "   Debugging tools provided include:                                      "
say "                                                                          "
say "        BRANCH:   show all paragraph entries.                             "
say "                                                                          "
say "        TRACE tv: will use value following TRACE to place the             "
say "                  execution in REXX TRACE Mode.                           "
say "                                                                          "
say "                                                                          "
say "   Debugging tools can be accessed in the following manner:               "
say "                                                                          "
say "        TSO "ex_nam"  parameters     ((  debug-options                    "
say "                                                                          "
say "   For example:                                                           "
say "                                                                          "
say "        TSO "ex_nam"  (( MONITOR TRACE ?R                                 "

if sysvar("SYSISPF") = "ACTIVE" then,
   address ISPEXEC "CONTROL DISPLAY REFRESH"
exit                                   /*@ HELP                      */
/*
.  ----------------------------------------------------------------- */
BRANCH: Procedure expose,              /*@                           */
        sigl exec_name
   rc = trace("O")                     /* we do not want to see this */
   arg brparm .

   origin = sigl                       /* where was I called from ?  */
   do currln = origin to 1 by -1       /* inch backward to label     */
      if Right(Word(Sourceline(currln),1),1) = ":" then do
         parse value sourceline(currln) with pgfname ":" .  /* Label */
         leave ; end                   /*                name        */
   end                                 /* currln                     */

   select
      when brparm = "NAME" then return(pgfname) /* Return full name  */
      when brparm = "ID"      then do           /* wants the prefix  */
         parse var pgfname pgfpref "_" .        /* get the prefix    */
         return(pgfpref)
         end                           /* brparm = "ID"              */
      otherwise
         say left(sigl,6) left(pgfname,40) exec_name "Time:" time("L")
   end                                 /* select                     */

return                                 /*@ BRANCH                    */
/*
.  ----------------------------------------------------------------- */
DUMP_QUEUE:                            /*@ Take whatever is in stack */
   rc = trace("O")                     /*  and write to the screen   */
   address TSO
   arg mode .

   "QSTACK"                            /* how many stacks?           */
   stk2dump    = rc - tk_init_stacks   /* remaining stacks           */
   if stk2dump = 0 & queued() = 0 then return
   if mode <> "QUIET" then,
   say "Total Stacks" rc ,             /* rc = #of stacks            */
    "   Begin Stacks" tk_init_stacks , /* Stacks present at start    */
    "   Excess Stacks to dump" stk2dump

   do dd = rc to tk_init_stacks by -1  /* empty each one.            */
      if mode <> "QUIET" then,
      say "Processing Stack #" dd "   Total Lines:" queued()
      do queued();parse pull line;say line;end /* pump to the screen */
      "DELSTACK"                       /* remove stack               */
   end                                 /* dd = 1 to rc               */

return                                 /*@ DUMP_QUEUE                */
/* Handle CLIST-form keywords             added 20020513
.  ----------------------------------------------------------------- */
CLKWD: Procedure expose info           /*@ hide all except info      */
   arg kw
   kw = kw"("                          /* form is 'KEY(DATA)'        */
   kw_pos = Pos(kw,info)               /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   rtpt   = Pos(") ",info" ",kw_pos)   /* locate end-paren           */
   slug   = Substr(info,kw_pos,rtpt-kw_pos+1)     /* isolate         */
   info   = Delstr(info,kw_pos,rtpt-kw_pos+1)     /* excise          */
   parse var slug (kw)     slug        /* drop kw                    */
   slug   = Reverse(Substr(Reverse(Strip(slug)),2))
return slug                            /*@CLKWD                      */
/* Handle multi-word keys 20020513
.  ----------------------------------------------------------------- */
KEYWD: Procedure expose info           /*@ hide all vars, except info*/
   arg kw
   kw_pos = wordpos(kw,info)           /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   kw_val = word(info,kw_pos+Words(kw))/* get the next word          */
   info   = Delword(info,kw_pos,2)     /* remove both                */
return kw_val                          /*@ KEYWD                     */
/*
.  ----------------------------------------------------------------- */
KEYPHRS: Procedure expose,             /*@                           */
         info helpmsg exec_name        /*  except these three        */
   arg kp
   wp    = wordpos(kp,info)            /* where is it?               */
   if wp = 0 then return ""            /* not found                  */
   front = subword(info,1,wp-1)        /* everything before kp       */
   back  = subword(info,wp+1)          /* everything after kp        */
   parse var back dlm back             /* 1st token must be 2 bytes  */
   if length(dlm) <> 2 then            /* Must be two bytes          */
      helpmsg = helpmsg,
         "Invalid length for delimiter("dlm") with KEYPHRS("kp")"
   if wordpos(dlm,back) = 0 then       /* search for ending delimiter*/
      helpmsg = helpmsg,
         "No matching second delimiter("dlm") with KEYPHRS("kp")"
   if helpmsg <> "" then call HELP     /* Something is wrong         */
   parse var back kpval (dlm) back     /* get everything b/w delim   */
   info =  front back                  /* restore remainder          */
return Strip(kpval)                    /*@ KEYPHRS                   */
/*
.  ----------------------------------------------------------------- */
NOVALUE:                               /*@                           */
   say exec_name "raised NOVALUE at line" sigl
   say " "
   say "The referenced variable is" condition("D")
   say " "
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ NOVALUE                   */
/*
.  ----------------------------------------------------------------- */
SHOW_SOURCE:                           /*@                           */
   call DUMP_QUEUE                     /* Spill contents of stacks  -*/
   if sourceline() <> "0" then         /* to screen                  */
      say sourceline(zsigl)
   rc =  trace("?R")
   nop
   exit                                /*@ SHOW_SOURCE               */
/*
.  ----------------------------------------------------------------- */
SS: Procedure                          /*@ Show Source               */
   arg  ssbeg  ssend  .
   if ssend = "" then ssend = 10
   if \datatype(ssbeg,"W") | \datatype(ssend,"W") then return
   ssend = ssbeg + ssend
   do ssii = ssbeg to ssend ; say Strip(sourceline(ssii),'T') ; end
return                                 /*@ SS                        */
/*
.  ----------------------------------------------------------------- */
SWITCH: Procedure expose info          /*@                           */
   arg kw
   sw_val = Wordpos(kw,info) > 0       /* exists = 1; not found = 0  */
   if sw_val then                      /* exists                     */
      info = Delword(info,Wordpos(kw,info),1) /* remove it           */
return sw_val                          /*@ SWITCH                    */
/*
.  ----------------------------------------------------------------- */
SYNTAX:                                /*@                           */
   errormsg = exec_name "encountered REXX error" rc "in line" sigl":",
                        errortext(rc)
   say errormsg
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ SYNTAX                    */
/*
   Can call TRAPOUT.
.  ----------------------------------------------------------------- */
TOOLKIT_INIT:                          /*@                           */
   address TSO
   info = Strip(opts,"T",")")          /* clip trailing paren        */

   parse source  sys_id  how_invokt  exec_name  DD_nm  DS_nm,
                     as_invokt  cmd_env  addr_spc  usr_tokn

   parse value "" with  tv  helpmsg  .
   parse value 0   "ISR00000  YES"     "Error-Press PF1"    with,
               sw.  zerrhm    zerralrm  zerrsm

   if SWITCH("TRAPOUT") then do
      "TRAPOUT" exec_name parms "(( TRACE R" info
      exit
      end                              /* trapout                    */

   sw.nested    = sysvar("SYSNEST") = "YES"
   sw.batch     = sysvar("SYSENV")  = "BACK"
   sw.inispf    = sysvar("SYSISPF") = "ACTIVE"

   if Word(parms,1) = "?" then call HELP /* I won't be back          */

   "QSTACK" ; tk_init_stacks = rc      /* How many stacks?           */

   parse value SWITCH("BRANCH") SWITCH("MONITOR") SWITCH("NOUPDT") with,
               branch           monitor           noupdt    .

   parse value mvsvar("SYSNAME") sysvar("SYSNODE") with,
               #tk_cpu           node          .

   parse value KEYWD("TRACE")  "N"    with   tv  .
   tk_globalvars = "exec_name  tv  helpmsg  sw.  zerrhm  zerralrm ",
                   "zerrsm  zerrlm  tk_init_stacks  branch  monitor ",
                   "noupdt"

   call LOCAL_PREINIT                  /* for more opts             -*/

return                                 /*@ TOOLKIT_INIT              */
