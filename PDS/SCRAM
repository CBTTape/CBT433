/* REXX   SCRAM    Produce IEBCOPY-flattened versions of PO datasets
                   suitable for downloading to the PC as binary files.

                   The name is derived from Nuclear Engineering: to
                   'scram' a reactor is to shut it down as quickly as
                   possible.
           Use '(routine name)  ?' for HELP-text.

           Written by Frank Clarke  rexxhead@yahoo.com 1994

     Impact Analysis
.    SYSEXEC   TRAPOUT

     Modification History
     19960203 fxc enable LISTIN for alternate SCRAMSET;
                  add REXXSKEL;
     20211026 fxc REXXSKEL v.960119 -> v.20210402
     20230407 fxc adjust HELP
     20230806 fxc chg SYSPROC to SYSEXEC in Impact Analysis;
     20231216 fxc chg 4096 blksize to 0;
     20231226 fxc restore msgstat;
     20240308 fxc chg dollar-sign to @ everywhere;
     20240415 fxc DUMP_QUEUE quiet;

*/ arg argline
address TSO                            /* REXXSKEL ver.20210402      */
arg parms "((" opts

signal on syntax
signal on novalue

call TOOLKIT_INIT                      /* conventional start-up     -*/
rc = Trace("O"); rc = Trace(tv)
info   = parms                         /* to enable parsing          */

call A_INIT                            /*                           -*/

if Sysdsn(scram_ds) <> "OK" then exit
msgstat = Msg('off')

call B_ALLOC_FILES                     /*                           -*/
call C_SPOOL_DATA                      /*                           -*/

rc = Msg( msgstat )                    /* restore                    */

if \sw.0nested then call DUMP_QUEUE 'quiet' /*                      -*/
exit                                   /*@ SCRAM                     */
/*
.  ----------------------------------------------------------------- */
A_INIT:                                /*@                           */
   if branch  then call BRANCH
   address TSO

   parse value  KEYWD("LISTIN") "SCRAMSET" with scram_ds .
   drive = "A:"
   iam        = #tk_cpu"."userid()

   alloc.0   = "NEW CATALOG UNIT( SYSDA ) SPACE( 1 ) TRACKS",
               "RECFM( V B ) LRECL( 255 ) BLKSIZE( 0 ) "
   alloc.1   = "SHR"                   /* if it already exists...    */

   msg. = "??"
   msg.0000  = "OK"
   msg.0005  = "NC"
   msg.0009  = "MI"

return                                 /*@ A_INIT                    */
/*
   Create and fill PC control files:
   PULL.BAT    is manually shipped to the PC.  It pulls the other two.
   SCRAM.BAT   pulls the .XMI flat files
   SCZIP.BAT   executes PKZIP on the PC files
.  ----------------------------------------------------------------- */
B_ALLOC_FILES:                         /*@                           */
   if branch then call BRANCH
   address TSO

   outdsn   = "PULL.BAT"
   ldrc   = Listdsi(outdsn"  nodirectory norecall")
   if msg.sysreason = "MI" then "HDEL" outdsn
   tempstat = Sysdsn(outdsn) = "OK"    /* 1=exists, 0=missing        */
   "ALLOC FI(@DRV) DA("outdsn") REU" alloc.tempstat

   outdsn   = "SCRAM.BAT"
   ldrc   = Listdsi(outdsn"  nodirectory norecall")
   if msg.sysreason = "MI" then "HDEL" outdsn
   tempstat = Sysdsn(outdsn) = "OK"    /* 1=exists, 0=missing        */
   "ALLOC FI(@BAT) DA("outdsn") REU" alloc.tempstat
   push " receive" Left("%1"outdsn,16) Left(drive||outdsn,16),
             "ascii crlf"
   "EXECIO 1 DISKW @DRV       "

   outdsn   = "SCZIP.BAT"
   ldrc   = Listdsi(outdsn"  nodirectory norecall")
   if msg.sysreason = "MI" then "HDEL" outdsn
   tempstat = Sysdsn(outdsn) = "OK"    /* 1=exists, 0=missing        */
   "ALLOC FI(@ZIP) DA("outdsn") REU" alloc.tempstat
   push " receive" Left("%1"outdsn,16) Left(drive||outdsn,16),
             "ascii crlf"
   "EXECIO 1 DISKW @DRV (FINIS"

return                                 /*@ B_ALLOC_FILES             */
/*
.  ----------------------------------------------------------------- */
C_SPOOL_DATA:                          /*@                           */
   if branch then call BRANCH
   address TSO

   "ALLOC FI(@TMP) DA("scram_ds") SHR REU"
   "EXECIO * DISKR @TMP (FINIS"
   if monitor then say queued() "lines in SCRAMSET"

   ignore_rest = '0'
   do queued()
      pull input output pcspec .
      if input = "***" then ignore_rest = '1'
      if ignore_rest then iterate
      rc = Outtrap("xx.")
      call SHIPIT   input   output
      rc = Outtrap("off")
      if monitor then say "Converted" input "to" output "rc="xrc
/*    push " receive" Left(pcspec,16) Left(drive||output,16)  */
      push " receive     " pcspec  "     " drive||output
      "EXECIO 1 DISKW @BAT"
      parse var pcspec fn "." ft
/*    push " pkzip" Left("%1"fn".ZIP",16) Left(pcspec,16)  */
      push " pkzip"  "%1"fn".ZIP"  "      " pcspec
      "EXECIO 1 DISKW @ZIP"
   end                                 /* queued                     */
   "EXECIO 0 DISKW @BAT (FINIS"
   "EXECIO 0 DISKW @ZIP (FINIS"

return                                 /*@ C_SPOOL_DATA              */
/*
   Create the .XMI dataset for shipment to the PC
.  ----------------------------------------------------------------- */
SHIPIT:                                /*@                           */
   arg in out

   "XMIT" iam "DS("in") OUTDATASET("out") nol non nop noe sysout(w)"
   xrc = rc
/* "DELETE" out
   xrc = Max(rc,xrc)   */

return                                 /*@ SHIPIT                    */
/*
.  ----------------------------------------------------------------- */
LOCAL_PREINIT:                         /*@ customize opts            */
   if branch then call BRANCH
   address TSO

   monitor   = \SWITCH("QUIET")

return                                 /*@ LOCAL_PREINIT             */
/*   subroutines below LOCAL_PREINIT are not selected by SHOWFLOW    */
/*
.  ----------------------------------------------------------------- */
HELP:                                  /*@                           */
address TSO;"CLEAR"
if helpmsg <> "" then say helpmsg; say ""
ex_nam = Left(exec_name,8)             /* predictable size           */
say "  "ex_nam"      Compress selected libraries and ship off-site for safety. "
say "                Prepare .BAT files for use by a PC-based process to       "
say "                download and ZIP the compressed files.                    "
say "                                                                          "
say "  Syntax:   "ex_nam"  LISTIN   input                                      "
say "                                                                          "
say "            input     is the 'scramset' dataset.  If not specified it     "
say "                      defaults to 'SCRAMSET'.                             "
say "                                                                          "
say "                      The format of a scramset-dataset is:                "
say "                          3 columns of data;                              "
say "                          1st col: the MVS datasetname to be SCRAMmed     "
say "                          2nd col: an intermediate MVS name for the       "
say "                                compressed version                        "
say "                          3rd col: the PC filespec                        "
say "                      If the first token on any line is a series of three "
say "                      asterisks, processing stops at that line.           "
say "                                                                          "
"NEWSTACK"; pull; "CLEAR"; "DELSTACK"
say "                                                                          "
say "   Debugging tools provided include:                                      "
say "                                                                          "
say "        MONITOR:  displays key information throughout processing.         "
say "                  Displays most paragraph names upon entry.               "
say "                                                                          "
say "        NOUPDT:   by-pass all update logic.                               "
say "                                                                          "
say "        BRANCH:   show all paragraph entries.                             "
say "                                                                          "
say "        TRACE tv: will use value following TRACE to place                 "
say "                  the execution in REXX TRACE Mode.                       "
say "                                                                          "
say "   Debugging tools can be accessed in the following manner:               "
say "                                                                          "
say "        TSO" exec_name"  parameters  ((  debug-options                    "
say "                                                                          "
say "   For example:                                                           "
say "                                                                          "
say "        TSO" exec_name " (( MONITOR TRACE ?R                              "
if sysvar("SYSISPF") = "ACTIVE" then,
   address ISPEXEC "CONTROL DISPLAY REFRESH"
exit                                   /*@ HELP                      */
/*
.  ----------------------------------------------------------------- */
BRANCH: Procedure expose,              /*@                           */
        sigl exec_name
   rc = trace("O")                     /* we do not want to see this */
   arg brparm .

   origin = sigl                       /* where was I called from ?  */
   do currln = origin to 1 by -1       /* inch backward to label     */
      if Right(Word(Sourceline(currln),1),1) = ":" then do
         parse value sourceline(currln) with pgfname ":" .  /* Label */
         leave ; end                   /*                name        */
   end                                 /* currln                     */

   select
      when brparm = "NAME" then return(pgfname) /* Return full name  */
      when brparm = "ID"      then do           /* wants the prefix  */
         parse var pgfname pgfpref "_" .        /* get the prefix    */
         return(pgfpref)
         end                           /* brparm = "ID"              */
      otherwise
         say left(sigl,6) left(pgfname,40) exec_name "Time:" time("L")
   end                                 /* select                     */

return                                 /*@ BRANCH                    */
/*
.  ----------------------------------------------------------------- */
DUMP_QUEUE:                            /*@ Take whatever is in stack */
   rc = trace("O")                     /*  and write to the screen   */
   address TSO
   arg mode .

   "QSTACK"                            /* how many stacks?           */
   stk2dump    = rc - tk_init_stacks   /* remaining stacks           */
   if stk2dump = 0 & queued() = 0 then return
   if mode <> "QUIET" then,
   say "Total Stacks" rc ,             /* rc = #of stacks            */
    "   Begin Stacks" tk_init_stacks , /* Stacks present at start    */
    "   Excess Stacks to dump" stk2dump

   do dd = rc to tk_init_stacks by -1  /* empty each one.            */
      if mode <> "QUIET" then,
      say "Processing Stack #" dd "   Total Lines:" queued()
      do queued();parse pull line;say line;end /* pump to the screen */
      "DELSTACK"                       /* remove stack               */
   end                                 /* dd = 1 to rc               */

return                                 /*@ DUMP_QUEUE                */
/* Handle CLIST-form keywords             added 20020513
.  ----------------------------------------------------------------- */
CLKWD: Procedure expose info           /*@ hide all except info      */
   arg kw
   kw = kw"("                          /* form is 'KEY(DATA)'        */
   kw_pos = Pos(kw,info)               /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   rtpt   = Pos(") ",info" ",kw_pos)   /* locate end-paren           */
   slug   = Substr(info,kw_pos,rtpt-kw_pos+1)     /* isolate         */
   info   = Delstr(info,kw_pos,rtpt-kw_pos+1)     /* excise          */
   parse var slug (kw)     slug        /* drop kw                    */
   slug   = Reverse(Substr(Reverse(Strip(slug)),2))
return slug                            /*@CLKWD                      */
/* Handle multi-word keys 20020513
.  ----------------------------------------------------------------- */
KEYWD: Procedure expose info           /*@ hide all vars, except info*/
   arg kw
   kw_pos = wordpos(kw,info)           /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   kw_val = word(info,kw_pos+Words(kw))/* get the next word          */
   info   = Delword(info,kw_pos,2)     /* remove both                */
return kw_val                          /*@ KEYWD                     */
/*
.  ----------------------------------------------------------------- */
KEYPHRS: Procedure expose,             /*@                           */
         info helpmsg exec_name        /*  except these three        */
   arg kp
   wp    = wordpos(kp,info)            /* where is it?               */
   if wp = 0 then return ""            /* not found                  */
   front = subword(info,1,wp-1)        /* everything before kp       */
   back  = subword(info,wp+1)          /* everything after kp        */
   parse var back dlm back             /* 1st token must be 2 bytes  */
   if length(dlm) <> 2 then            /* Must be two bytes          */
      helpmsg = helpmsg,
         "Invalid length for delimiter("dlm") with KEYPHRS("kp")",
         info
   if wordpos(dlm,back) = 0 then       /* search for ending delimiter*/
      helpmsg = helpmsg,
         "No matching second delimiter("dlm") with KEYPHRS("kp")",
         info
   if helpmsg <> "" then call HELP     /* Something is wrong         */
   parse var back kpval (dlm) back     /* get everything b/w delim   */
   info =  front back                  /* restore remainder          */
return Strip(kpval)                    /*@ KEYPHRS                   */
/*
.  ----------------------------------------------------------------- */
NOVALUE:                               /*@                           */
   say exec_name "raised NOVALUE at line" sigl
   say " "
   say "The referenced variable is" condition("D")
   say " "
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ NOVALUE                   */
/*
.  ----------------------------------------------------------------- */
SHOW_SOURCE:                           /*@                           */
   call DUMP_QUEUE                     /* Spill contents of stacks  -*/
   if sourceline() <> "0" then         /* to screen                  */
      say sourceline(zsigl)
   rc =  trace("?R")
   nop
   exit                                /*@ SHOW_SOURCE               */
/*
.  ----------------------------------------------------------------- */
SS: Procedure                          /*@ Show Source               */
   arg  ssbeg  ssend  .
   if ssend = "" then ssend = 10
   if \datatype(ssbeg,"W") | \datatype(ssend,"W") then return
   ssend = ssbeg + ssend
   do ssii = ssbeg to ssend ; say Strip(sourceline(ssii),'T') ; end
return                                 /*@ SS                        */
/*
.  ----------------------------------------------------------------- */
SWITCH: Procedure expose info          /*@                           */
   arg kw
   sw_val = Wordpos(kw,info) > 0       /* exists = 1; not found = 0  */
   if sw_val then                      /* exists                     */
      info = Delword(info,Wordpos(kw,info),1) /* remove it           */
return sw_val                          /*@ SWITCH                    */
/*
.  ----------------------------------------------------------------- */
SYNTAX:                                /*@                           */
   errormsg = exec_name "encountered REXX error" rc "in line" sigl":",
                        errortext(rc)
   say errormsg
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ SYNTAX                    */
/*
   Can call TRAPOUT.
.  ----------------------------------------------------------------- */
TOOLKIT_INIT:                          /*@                           */
   address TSO
   info = Strip(opts,"T",")")          /* clip trailing paren        */

   parse source  sys_id  how_invokt  exec_name  DD_nm  DS_nm,
                     as_invokt  cmd_env  addr_spc  usr_tokn

   parse value "" with  tv  helpmsg  .
   parse value 0   "ISR00000  YES"     "Error-Press PF1"    with,
               sw.  zerrhm    zerralrm  zerrsm

   if SWITCH("TRAPOUT") then do
      "TRAPOUT" exec_name parms "(( TRACE R" info
      exit
      end                              /* trapout                    */

   sw.0nested   = sysvar("SYSNEST") = "YES"
   sw.0batch    = sysvar("SYSENV")  = "BACK"
   sw.0inispf   = sysvar("SYSISPF") = "ACTIVE"

   if Word(parms,1) = "?" then call HELP /* I won't be back          */

   "QSTACK" ; tk_init_stacks = rc      /* How many stacks?           */

   parse value SWITCH("BRANCH") SWITCH("MONITOR") SWITCH("NOUPDT") with,
               branch           monitor           noupdt    .

   parse value mvsvar("SYSNAME") sysvar("SYSNODE") with,
               #tk_cpu           node          .

   parse value KEYWD("TRACE")  "N"    with   tv  .
   tk_globalvars = "exec_name  tv  helpmsg  sw.  zerrhm  zerralrm ",
                   "zerrsm  zerrlm  tk_init_stacks  branch  monitor ",
                   "noupdt"

   call LOCAL_PREINIT                  /* for more opts             -*/

return                                 /*@ TOOLKIT_INIT              */
