/* REXX    SUPBR      SuperBrowse replacement
           Use '(routine name)  ?' for HELP-text.
   |**-***-***-***-***-***-***-***-***-***-***-***-***-***-***-***-**|
   |                                                                 |
   |          WARNING: EMBEDDED COMPONENTS.                          |
   |                      See text following TOOLKIT_INIT            |
   |                                                                 |
   |**-***-***-***-***-***-***-***-***-***-***-***-***-***-***-***-**|

           Written by Frank Clarke, rexxhead@yahoo.com

     Impact Analysis
.    SYSEXEC  SUPED

     Modification History
     19971126 fxc upgrade from undated REXXSKEL to v.19971030;
     19980309 fxc imbed panel ZSUPBROW
     19980811 fxc change BROWSE to VIEW
     19991129 fxc upgrade v.19971030 to v.19991109; new DEIMBED;
     20210408 fxc add help panel SUPEDH; upgrade REXXSKEL to v.20210402
     20230128 fxc use MEMLIST when no member is specified; allows user
                  to select Edit, View, Delete, &c.
     20230527 fxc re-enable edit-from-supbr
     20230608 fxc use &ZUP/&ZCONT
     20230613 fxc add DEL to DEIMBED ALLOC
     20230726 fxc adjust HELP;
     20230806 fxc chg SYSPROC to SYSEXEC in Impact Analysis;
     20240305 fxc align panel names;
     20240309 fxc change dollar-sign to @ everywhere;

*/ arg argline
address ISPEXEC                        /* REXXSKEL ver.20210402      */
arg parms "((" opts

signal on syntax
signal on novalue

call TOOLKIT_INIT                      /* conventional start-up     -*/
rc     = trace(tv)
info   = parms                         /* to enable parsing          */
if Sysvar("SYSISPF") = "NOT ACTIVE" then do
   address TSO
   arg argline
   "ISPSTART CMD("Sysvar("SYSICMD")  argline ")"
   exit
   end                                 /* ISPF not active            */

call A_INIT                            /*                           -*/
call B_ISPF_OPS                        /*                           -*/

exit                                   /*@ SUPBR                     */
/*
.  ----------------------------------------------------------------- */
A_INIT:                                /*@                           */
   if branch then call BRANCH
   address TSO

   parse value "" with bore  member  zopt
   func    = "VIEW"
   altfunc = "EDIT"
   op      = func

return                                 /*@ A_INIT                    */
/*
.  ----------------------------------------------------------------- */
B_ISPF_OPS:                            /*@                           */
   if branch then call BRANCH
   address ISPEXEC

   call DEIMBED                        /* extract panel              */
   dd = ""
   do Words(ddnlist)                   /* each LIBDEF DD             */
      parse value ddnlist dd  with  dd ddnlist
      @ddn   = @ddn.dd                 /* PLIB322 <- PLIB            */
      "LIBDEF  ISP"dd "LIBRARY  ID("@ddn") STACK"
   end
   ddnlist = ddnlist dd

   "CONTROL ERRORS RETURN"
   do forever
      "DISPLAY PANEL(ZSUPBROW)"        /* edit or browse             */
      if rc > 0 then leave

      if bmember = "" then meminfo = ""
      else meminfo = "("bmember")"

      if bore  = "Y" then,             /* wants to Edit              */
         op   = altfunc
      else op = func

      if zopt = "" then iterate
      if zopt < "I" then do            /* 3-level set                */
         ed_dsn =,
              Value("BPRJ"zopt)"."Value("BLIB"zopt)"."Value("BTYP"zopt)
         ed_dsn = Strip(ed_dsn,,".")   /* lop trailing dots          */
         end ; else,                   /* 3-level set                */
      if zopt > "H" then do            /* n-level set                */
         ed_dsn = Value("BDSN"zopt)
         if Left(ed_dsn,1) = "'" then,
            ed_dsn = Strip(ed_dsn,,"'")
         else ed_dsn = Userid()"."ed_dsn
         end                           /* n-level set                */
      call D_GET_DSORG                 /*                           -*/

      if sysdsorg = "PS" then do
         (OP) "DATASET('"ed_dsn"') PROFILE(DEFAULT)"
         call M_MSG(rc)
         end ; else,                   /* sequential                 */
      if meminfo <> "" then do         /* single member              */
         (OP) "DATASET('"ed_dsn||meminfo"') PROFILE(DEFAULT)"
         call M_MSG(rc)
         end                           /* single member              */
      else do                          /* no member                  */
         dflt = Left( op,1 )
         "LMINIT DATASET('"ed_dsn"') DATAID(SUP00)"
         "MEMLIST DATAID("sup00") FIELD(9) DEFAULT("dflt")"
         "LMFREE DATAID("sup00")"
         end                           /* no member                  */
      zopt = ''
   end                                 /* forever                    */

   dd = ""
   do Words(ddnlist)                   /* each LIBDEF DD             */
      parse value ddnlist dd  with  dd ddnlist
      "LIBDEF  ISP"dd
   end

return                                 /*@ B_ISPF_OPS                */
/*
.  ----------------------------------------------------------------- */
D_GET_DSORG:                           /*@                           */
   if branch then call BRANCH
   address TSO

   rc = LISTDSI("'"ed_dsn"'")

return                                 /*@ D_GET_DSORG               */
/*
.  ----------------------------------------------------------------- */
M_MSG:                                 /*@                           */
   if branch then call BRANCH
   address ISPEXEC
   arg retcode .

   if op = "EDIT" then do              /* Edit                       */
      if bmember = "" then do          /* No member                  */
         if rc = 0 then do; zerrsm="Dataset saved"
                            zerrlm="Dataset saved as" ed_dsn; end
         else,
         if rc = 4 then do; zerrsm="Dataset not changed";
             zerrlm="Dataset was not changed or Edit was cancelled"; end
         end                           /* No member                  */
      else do                          /* Member                     */
         if rc = 0 then do; zerrsm="Member saved";
                          zerrlm=bmember "has been saved in" ed_dsn; end
         else,
         if rc = 4 then do; zerrsm="Member not changed";
             zerrlm=bmember "was not changed or Edit was cancelled"; end
         end                           /* Member                     */
      end                              /* Edit                       */
   else do                             /* View                       */
      if bmember = "" then do          /* No member                  */
         if rc = 0 then do; zerrsm="View completed";
                            zerrlm=ed_dsn "was viewed"; end
         end                           /* No member                  */
      else do                          /* Member                     */
         if rc = 0 then do; zerrsm="Member viewed"
                          zerrlm=bmember "viewed in dataset" ed_dsn; end
         else,
         if rc = 4 then do
            zerrsm="Member not found or in use"
            zerrlm=bmember "was not found in" ed_dsn,
                           "or was in use by another process"; end
         end                           /* Member                     */
      end                              /* View                       */
   if zerrsm <> "" then,
      "SETMSG MSG(ISRZ002)"

return                                 /*@ M_MSG                     */
/*
   Parse out the embedded components at the back of the source code.
.  ----------------------------------------------------------------- */
DEIMBED: Procedure expose,             /*@                           */
   (tk_globalvars)  ddnlist  @ddn.  daid.

   address TSO

   fb80po.0  = "NEW DEL UNIT(VIO) SPACE(5 5) TRACKS DIR(40)",
                   "RECFM(F B) LRECL(80) BLKSIZE(0)"
   parse value ""   with  ddnlist @ddn.  daid.

   lastln   = sourceline()
   currln   = lastln                   /*                            */
   if Left(sourceline(currln),2) <> "*/" then return

   currln = currln - 1                 /* previous line              */
   "NEWSTACK"
   address ISPEXEC
   do while sourceline(currln) <> "/*"
      text = sourceline(currln)        /* save with a short name !   */
      if Left(text,3) = ")))" then do  /* package the queue          */
         parse var text ")))" ddn mbr .   /* PLIB PANL001  maybe     */
         if Pos(ddn,ddnlist) = 0 then do  /* doesn't exist           */
            ddnlist = ddnlist ddn      /* keep track                 */
            @ddn = ddn || Random(999)
            @ddn.ddn = @ddn
            address TSO "ALLOC FI("@ddn")" fb80po.0
            "LMINIT DATAID(DAID) DDNAME("@ddn")"
            daid.ddn = daid
            end
         daid = daid.ddn
         "LMOPEN DATAID("daid") OPTION(OUTPUT)"
         do queued()
            parse pull line
            "LMPUT DATAID("daid") MODE(INVAR) DATALOC(LINE) DATALEN(80)"
         end
         "LMMADD DATAID("daid") MEMBER("mbr")"
         "LMCLOSE DATAID("daid")"
         end                           /* package the queue          */
      else push text                   /* onto the top of the stack  */
      currln = currln - 1              /* previous line              */
   end                                 /* while                      */
   address TSO "DELSTACK"

return                                 /*@ DEIMBED                   */
/*
.  ----------------------------------------------------------------- */
LOCAL_PREINIT:                         /*@ customize opts            */
   address TSO


return                                 /*@ LOCAL_PREINIT             */
/*
.  ----------------------------------------------------------------- */
HELP:                                  /*@                           */
address TSO;"CLEAR"
if helpmsg <> "" then say helpmsg; say ""
ex_nam = Left(exec_name,8)             /* predictable size           */
say "                                                                          "
say "  "ex_nam"      provides an easy interface for browsing your favorite     "
say "                datasets.                                                 "
say "                                                                          "
say "  Syntax:   "ex_nam"  no parms                                            "
"NEWSTACK"; pull; "CLEAR" ; "DELSTACK"
say "                                                                          "
say "   Debugging tools provided include:                                      "
say "                                                                          "
say "        MONITOR   displays key information throughout processing.         "
say "                  Displays most paragraph names upon entry.               "
say "                                                                          "
say "        BRANCH    show all paragraph entries.                             "
say "                                                                          "
say "        TRACE tv  will use value following TRACE to place the execution in"
say "                  REXX TRACE Mode.                                        "
say "                                                                          "
say "                                                                          "
say "   Debugging tools can be accessed in the following manner:               "
say "                                                                          "
say "        TSO "ex_nam"  parameters     ((  debug-options                    "
say "                                                                          "
say "   For example                                                            "
say "                                                                          "
say "        TSO "ex_nam"  (( MONITOR TRACE ?R                                 "
if sysvar("SYSISPF") = "ACTIVE" then,
   address ISPEXEC "CONTROL DISPLAY REFRESH"
exit                                   /*@ HELP                      */
/*
.  ----------------------------------------------------------------- */
BRANCH: Procedure expose,              /*@                           */
        sigl exec_name
   rc = trace("O")                     /* we do not want to see this */
   arg brparm .

   origin = sigl                       /* where was I called from ?  */
   do currln = origin to 1 by -1       /* inch backward to label     */
      if Right(Word(Sourceline(currln),1),1) = ":" then do
         parse value sourceline(currln) with pgfname ":" .  /* Label */
         leave ; end                   /*                name        */
   end                                 /* currln                     */

   select
      when brparm = "NAME" then return(pgfname) /* Return full name  */
      when brparm = "ID"      then do           /* wants the prefix  */
         parse var pgfname pgfpref "_" .        /* get the prefix    */
         return(pgfpref)
         end                           /* brparm = "ID"              */
      otherwise
         say left(sigl,6) left(pgfname,40) exec_name "Time:" time("L")
   end                                 /* select                     */

return                                 /*@ BRANCH                    */
/*
.  ----------------------------------------------------------------- */
DUMP_QUEUE:                            /*@ Take whatever is in stack */
   rc = trace("O")                     /*  and write to the screen   */
   address TSO

   "QSTACK"                            /* how many stacks?           */
   stk2dump    = rc - tk_init_stacks   /* remaining stacks           */
   if stk2dump = 0 & queued() = 0 then return
   say "Total Stacks" rc ,             /* rc = #of stacks            */
    "   Begin Stacks" tk_init_stacks , /* Stacks present at start    */
    "   Excess Stacks to dump" stk2dump

   do dd = rc to tk_init_stacks by -1  /* empty each one.            */
      say "Processing Stack #" dd "   Total Lines:" queued()
      do queued();pull line;say line;end /* pump to the screen       */
      "DELSTACK"                       /* remove stack               */
   end                                 /* dd = 1 to rc               */

return                                 /*@ DUMP_QUEUE                */
/* Handle CLIST-form keywords             added 20020513
.  ----------------------------------------------------------------- */
CLKWD: Procedure expose info           /*@ hide all except info      */
   arg kw
   kw = kw"("                          /* form is 'KEY(DATA)'        */
   kw_pos = Pos(kw,info)               /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   rtpt   = Pos(") ",info" ",kw_pos)   /* locate end-paren           */
   slug   = Substr(info,kw_pos,rtpt-kw_pos+1)     /* isolate         */
   info   = Delstr(info,kw_pos,rtpt-kw_pos+1)     /* excise          */
   parse var slug (kw)     slug        /* drop kw                    */
   slug   = Reverse(Substr(Reverse(Strip(slug)),2))
return slug                            /*@CLKWD                      */
/* Handle multi-word keys 20020513
.  ----------------------------------------------------------------- */
KEYWD: Procedure expose info           /*@ hide all vars, except info*/
   arg kw
   kw_pos = wordpos(kw,info)           /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   kw_val = word(info,kw_pos+Words(kw))/* get the next word          */
   info   = Delword(info,kw_pos,2)     /* remove both                */
return kw_val                          /*@ KEYWD                     */
/*
.  ----------------------------------------------------------------- */
KEYPHRS: Procedure expose,             /*@                           */
         info helpmsg exec_name        /*  except these three        */
   arg kp
   wp    = wordpos(kp,info)            /* where is it?               */
   if wp = 0 then return ""            /* not found                  */
   front = subword(info,1,wp-1)        /* everything before kp       */
   back  = subword(info,wp+1)          /* everything after kp        */
   parse var back dlm back             /* 1st token must be 2 bytes  */
   if length(dlm) <> 2 then            /* Must be two bytes          */
      helpmsg = helpmsg,
         "Invalid length for delimiter("dlm") with KEYPHRS("kp")"
   if wordpos(dlm,back) = 0 then       /* search for ending delimiter*/
      helpmsg = helpmsg,
         "No matching second delimiter("dlm") with KEYPHRS("kp")"
   if helpmsg <> "" then call HELP     /* Something is wrong         */
   parse var back kpval (dlm) back     /* get everything b/w delim   */
   info =  front back                  /* restore remainder          */
return Strip(kpval)                    /*@ KEYPHRS                   */
/*
.  ----------------------------------------------------------------- */
NOVALUE:                               /*@                           */
   say exec_name "raised NOVALUE at line" sigl
   say " "
   say "The referenced variable is" condition("D")
   say " "
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ NOVALUE                   */
/*
.  ----------------------------------------------------------------- */
SHOW_SOURCE:                           /*@                           */
   call DUMP_QUEUE                     /* Spill contents of stacks  -*/
   if sourceline() <> "0" then         /* to screen                  */
      say sourceline(zsigl)
   rc =  trace("?R")
   nop
   exit                                /*@ SHOW_SOURCE               */
/*
.  ----------------------------------------------------------------- */
SS: Procedure                          /*@ Show Source               */
   arg  ssbeg  ssend  .
   if ssend = "" then ssend = 10
   if \datatype(ssbeg,"W") | \datatype(ssend,"W") then return
   ssend = ssbeg + ssend
   do ssii = ssbeg to ssend ; say Strip(sourceline(ssii),'T') ; end
return                                 /*@ SS                        */
/*
.  ----------------------------------------------------------------- */
SWITCH: Procedure expose info          /*@                           */
   arg kw
   sw_val = Wordpos(kw,info) > 0       /* exists = 1; not found = 0  */
   if sw_val then                      /* exists                     */
      info = Delword(info,Wordpos(kw,info),1) /* remove it           */
return sw_val                          /*@ SWITCH                    */
/*
.  ----------------------------------------------------------------- */
SYNTAX:                                /*@                           */
   errormsg = exec_name "encountered REXX error" rc "in line" sigl":",
                        errortext(rc)
   say errormsg
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ SYNTAX                    */
/*
   Can call TRAPOUT.
.  ----------------------------------------------------------------- */
TOOLKIT_INIT:                          /*@                           */
   address TSO
   info = Strip(opts,"T",")")          /* clip trailing paren        */

   parse source  sys_id  how_invokt  exec_name  DD_nm  DS_nm,
                     as_invokt  cmd_env  addr_spc  usr_tokn

   parse value "" with  tv  helpmsg  .
   parse value 0   "ISR00000  YES"     "Error-Press PF1"    with,
               sw.  zerrhm    zerralrm  zerrsm

   if SWITCH("TRAPOUT") then do
      "TRAPOUT" exec_name parms "(( TRACE R" info
      exit
      end                              /* trapout                    */

   sw.nested    = sysvar("SYSNEST") = "YES"
   sw.batch     = sysvar("SYSENV")  = "BACK"
   sw.inispf    = sysvar("SYSISPF") = "ACTIVE"

   if Word(parms,1) = "?" then call HELP /* I won't be back          */

   "QSTACK" ; tk_init_stacks = rc      /* How many stacks?           */

   parse value SWITCH("BRANCH") SWITCH("MONITOR") SWITCH("NOUPDT") with,
               branch           monitor           noupdt    .

   parse value mvsvar("SYSNAME") sysvar("SYSNODE") with,
               #tk_cpu           node          .

   parse value KEYWD("TRACE")  "N"    with   tv  .
   tk_globalvars = "exec_name  tv  helpmsg  sw.  zerrhm  zerralrm ",
                   "zerrsm  zerrlm  tk_init_stacks  branch  monitor ",
                   "noupdt"

   call LOCAL_PREINIT                  /* for more opts             -*/

return                                 /*@ TOOLKIT_INIT              */
/*
))) PLIB     ZSUPBROW  ..
)ATTR
 % TYPE( TEXT   ) INTENS( HIGH ) SKIP( ON )
 + TYPE( TEXT   ) INTENS( LOW  ) SKIP( ON )
 @ TYPE( TEXT   ) INTENS( LOW  ) COLOR( YELLOW )
)BODY EXPAND(||) WIDTH(&ZSCREENW)
@Unsupported ------------------% Super Browse @---------------------------------
%SELECT LIBRARY%===>_ZOPT
                                                            +%EDIT DATA ?==>_Z+
%SELECT MEMBER %===>_BMEMBER +  (Blank for member list)
+
+ISPF LIBRARIES:
%A+ PROJECT%===>_BPRJA   +%B+%===>_BPRJB   +%C+%===>_BPRJC   +%D+%===>_BPRJD   +
+   LIBRARY%===>_BLIBA   +   %===>_BLIBB   +   %===>_BLIBC   +   %===>_BLIBD   +
+   TYPE   %===>_BTYPA   +   %===>_BTYPB   +   %===>_BTYPC   +   %===>_BTYPD   +
%
%E+ PROJECT%===>_BPRJE   +%F+%===>_BPRJF   +%G+%===>_BPRJG   +%H+%===>_BPRJH   +
+   LIBRARY%===>_BLIBE   +   %===>_BLIBF   +   %===>_BLIBG   +   %===>_BLIBH   +
+   TYPE   %===>_BTYPE   +   %===>_BTYPF   +   %===>_BTYPG   +   %===>_BTYPH   +
+
+OTHER PARTITIONED OR SEQUENTIAL DATASETS:
%I+ DATASET NAME %===>_BDSNI                                                   +
%J+ DATASET NAME %===>_BDSNJ                                                   +
%K+ DATASET NAME %===>_BDSNK                                                   +
%L+ DATASET NAME %===>_BDSNL                                                   +
%M+ DATASET NAME %===>_BDSNM                                                   +
%N+ DATASET NAME %===>_BDSNN                                                   +
%O+ DATASET NAME %===>_BDSNO                                                   +
%P+ DATASET NAME %===>_BDSNP                                                   +
%Q+ DATASET NAME %===>_BDSNQ                                                   +
)INIT
   IF (&MSG = ' ')
      .MSG = &MSG
   .ZVARS  = '(BORE)'
   .CURSOR = ZOPT
   .HELP   = SUPBRH
   &BOCNL  = N
   &BSVOL  = ' '
   &BSMEMBER  = ' '
   &ZSEL   = ' '
)REINIT
   .CURSOR = ZOPT
)PROC
   &MSG = ' '
   VER(&ZOPT,LIST,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q)
   IF (&ZOPT=A)
      VER(&BPRJA,NONBLANK)
      VER(&BLIBA,NONBLANK)
   IF (&ZOPT=B)
      VER(&BPRJB,NONBLANK)
      VER(&BLIBB,NONBLANK)
   IF (&ZOPT=C)
      VER(&BPRJC,NONBLANK)
      VER(&BLIBC,NONBLANK)
   IF (&ZOPT=D)
      VER(&BPRJD,NONBLANK)
      VER(&BLIBD,NONBLANK)
   IF (&ZOPT=E)
      VER(&BPRJE,NONBLANK)
      VER(&BLIBE,NONBLANK)
   IF (&ZOPT=F)
      VER(&BPRJF,NONBLANK)
      VER(&BLIBF,NONBLANK)
   IF (&ZOPT=G)
      VER(&BPRJG,NONBLANK)
      VER(&BLIBG,NONBLANK)
   IF (&ZOPT=H)
      VER(&BPRJH,NONBLANK)
      VER(&BLIBH,NONBLANK)
   IF (&ZOPT=I)
      VER(&BDSNI,NONBLANK)
   IF (&ZOPT=J)
      VER(&BDSNJ,NONBLANK)
   IF (&ZOPT=K)
      VER(&BDSNK,NONBLANK)
   IF (&ZOPT=L)
      VER(&BDSNL,NONBLANK)
   IF (&ZOPT=M)
      VER(&BDSNM,NONBLANK)
   IF (&ZOPT=N)
      VER(&BDSNN,NONBLANK)
   IF (&ZOPT=O)
      VER(&BDSNO,NONBLANK)
   IF (&ZOPT=P)
      VER(&BDSNP,NONBLANK)
   IF (&ZOPT=Q)
      VER(&BDSNQ,NONBLANK)
   VPUT (
          BPRJA BLIBA BTYPA BPRJB BLIBB BTYPB BPRJC BLIBC BTYPC
          BPRJD BLIBD BTYPD BPRJE BLIBE BTYPE BPRJF BLIBF BTYPF
          BPRJG BLIBG BTYPG BPRJH BLIBH BTYPH BDSNI BDSNJ BDSNK
          BDSNL BDSNM BDSNN BDSNO BDSNP BDSNQ
        ) PROFILE
)END
))) PLIB     SUPBRH    ..
)ATTR
  % TYPE( TEXT   ) INTENS( HIGH ) SKIP( ON )
  + TYPE( TEXT   ) INTENS( LOW  ) SKIP( ON )
  _ TYPE( INPUT  ) INTENS( HIGH )
  ! TYPE( OUTPUT ) INTENS( HIGH ) SKIP( ON )
  @ TYPE( OUTPUT ) INTENS( LOW  ) SKIP( ON )
)BODY EXPAND(||) WIDTH(&ZSCREENW)
%TUTORIAL |-| SuperBrowse |-| TUTORIAL
%Next Selection ===>_ZCMD

+
       SuperBrowse provides an easy way to get directly to your most commonly
       needed datasets.  The panel provides for eight (8) three-level datasets
       and nine (9) others.

       Invoke SuperBrowse as 'SUPBR;D' to get to the 'D' dataset directly.
       (Others by extension.)

       It is possible to Edit reather than Browse (View) by selecting the
       option on the panel.

)PROC
   &ZUP   = SUPBRH
   &ZCONT = SUPBRH
)END
*/
